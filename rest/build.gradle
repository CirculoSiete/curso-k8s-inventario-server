
apply plugin: 'org.springframework.boot'
apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin

jar {
  baseName = 'service'
  version = ''
}

dependencies {
  compile project(':contract')
  compile project(':service')
  compile("org.springframework.boot:spring-boot-starter-web")
  compile 'org.springframework.cloud:spring-cloud-starter-zipkin'

  testCompile 'com.h2database:h2:1.4.196'

  runtime 'org.postgresql:postgresql:42.1.4'
}

task createDockerfile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile) {
  destFile = project.file('build/libs/Dockerfile')
  from 'openjdk:8u131-jre-alpine'
  maintainer 'Domingo Suarez Torres "domingo@circulosiete.com"'
  copyFile 'service.jar', '/opt/service.jar'
  exposePort 8090
  entryPoint 'java', "-Djava.awt.headless=true", "-Xms128m", "-Xmx128m", '-jar', '/opt/service.jar'
}

task buildImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
  dependsOn createDockerfile
  inputDir = createDockerfile.destFile.parentFile
  tag = 'circulo7/cursos-k8s-product-rest:' + project.version.toLowerCase()
}

task pushImage(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
  dependsOn buildImage
  imageName = buildImage.tag
}

createDockerfile.dependsOn bootRepackage
